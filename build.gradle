import java.util.concurrent.TimeUnit

buildscript {
    repositories {
        mavenCentral()
		jcenter()
		maven {
			name = "gt"
			url = "https://gregtech.overminddl1.com/"
		}
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io"
        }
    }
    dependencies {
        classpath "com.github.GTNewHorizons:ForgeGradle:1.2.4"
    }
}

plugins {
    id("org.ajoberstar.grgit") version("3.1.1")
}
apply plugin: "java"
apply plugin: "idea"
apply plugin: "forge"
apply plugin: "signing"

idea {
    module {
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.coremod.version}"
group = "com.dreammaster"
archivesBaseName = "GTNewHorizonsCoreMod"

minecraft {
    version = "${config.minecraft.version}-${config.forge.version}-${config.minecraft.version}"
    runDir = "eclipse"

    replaceIn "src/main/java/com/dreammaster/lib/Refstrings.java"
    replace "GRADLETOKEN_VERSION", ((String) project.version).split("-")[1]
}

repositories {
    maven {
        name = "UsrvDE"
        url = "http://jenkins.usrv.eu:8081/nexus/content/repositories/releases/"
    }
    maven {
        name = "gt"
        url = "https://gregtech.overminddl1.com/"
    }
    maven {
        url = "https://maven.ic2.player.to/"
        name = "ic2"
    }
    maven {
        url "https://cursemaven.com"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    ivy {
        name = "gtnh_download_source"
        artifactPattern("http://downloads.gtnewhorizons.com/Mods_for_Jenkins/[module]-[revision].[ext]")
    }
    ivy {
        name = "gtnh_download_source_stupid_underscore_typo"
        artifactPattern("http://downloads.gtnewhorizons.com/Mods_for_Jenkins/[module]_[revision].[ext]")
    }
}
configurations.all {
    resolutionStrategy.cacheChangingModulesFor(0, TimeUnit.SECONDS)
    setTransitive(false)
}

dependencies {
    compile "eu.usrv:YAMCore:${config.minecraft.version}-${config.yamcore.version}:deobf"
    compile "net.industrial-craft:industrialcraft-2:${config.ic2.version}:dev"
    compile "thaumcraft:Thaumcraft:1.7.10-4.2.3.5:dev"
    compile "com.mod-buildcraft:buildcraft:7.1.23:dev"

    //downloads.gtnewhorizons.com
    compile ":WitcheryExtras:1.0.1-dev"
    compile ":witchery:1.7.10-0.24.1:deobf"
    //jitpack
    compile "com.github.GTNewHorizons:baubles:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:Hardcore-Ender-Expansion:master-SNAPSHOT:dev"
    compileOnly "com.github.GTNewHorizons:AppleCore:master-SNAPSHOT:dev"
    runtime "curse.maven:cofh-lib-220333:2388748"
    compile "com.github.GTNewHorizons:EnderIO:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:EnderCore:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:CodeChickenLib:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:CodeChickenCore:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:NotEnoughItems:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:Railcraft:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:twilightforest:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:GT5-Unofficial:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:GTplusplus:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:StructureLib:master-SNAPSHOT:dev"
    compile "com.github.GTNewHorizons:Mantle:master-SNAPSHOT:dev"
    compile("com.github.GTNewHorizons:TinkersConstruct:master-SNAPSHOT:dev") {
        setChanging(true)
    }
    compile("com.github.GTNewHorizons:Applied-Energistics-2-Unofficial:master-SNAPSHOT:dev") {
        setChanging(true)
    }
    compile("com.github.GTNewHorizons:ForestryMC:master-SNAPSHOT:dev") {
        setChanging(true)
    }
    runtime "com.github.GTNewHorizons:ironchest:master-SNAPSHOT:dev"
    runtime "com.github.GTNewHorizons:Galacticraft:master-SNAPSHOT:dev"
    compile("com.github.GTNewHorizons:GalacticGregGT5:master-SNAPSHOT") {
        setChanging(true)
    }
    compile("com.github.GTNewHorizons:bartworks:master-SNAPSHOT:dev") {
        setChanging(true)
    }
}

processResources
{
    def cleanVersion = ((String) project.version).split("-")[1]
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", cleanVersion
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version":cleanVersion, "mcversion":project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

task devJar(type: Jar) {
    from sourceSets.main.output
    getArchiveClassifier().set("dev")
    manifest {
        attributes "FMLCorePlugin": "com.dreammaster.coremod.DepLoader","FMLCorePluginContainsFMLMod": "true"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    getArchiveClassifier().set("sources")
}

artifacts {
    archives devJar
    archives sourceJar
}

task signJar(dependsOn: "reobf"){
    doLast {
        ant.signjar(
            destDir: jar.getDestinationDirectory(),
            jar: jar.getArchiveFile(),
            alias: findProperty("keyStoreAlias") ?: "",
            keystore: findProperty("keyStore") ?: "",
            storepass: findProperty("keyStorePass") ?: "",
            digestalg: findProperty("signDigestAlg") ?: "",
            tsaurl: findProperty("signTSAurl") ?: "",
            verbose: true
            )
    }
}

jar {
    manifest {
        attributes "FMLCorePlugin": "com.dreammaster.coremod.DreamCoreMod","FMLCorePluginContainsFMLMod": "true"
    }
}
